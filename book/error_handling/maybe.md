# Maybe

A medida que uses Elm te topar√°s con el tipo [`Maybe`][Maybe] muy frecuentemente. Est√° definido as√≠:

```elm
type Maybe a
    = Just a
    | Nothing



-- Just 3.14 : Maybe Float
-- Just "hi" : Maybe String
-- Just True : Maybe Bool
-- Nothing   : Maybe a
```

Es un tipo con dos variantes. O bien tienes nada (`Nothing`), o tienes un valor (`Just`). La variable de tipo te permite tener `Maybe Float` o tal vez `Maybe String`, dependiendo del valor espec√≠fico.

Este tipo es √∫til en dos situaciones principalmente: funciones parciales y campos opcionales.

[Maybe]: https://package.elm-lang.org/packages/elm-lang/core/latest/Maybe#Maybe

## Funciones parciales

A veces necesitamos una funci√≥n que da una respuesta a ciertos argumentos, pero no a otros. Muchos se encuentran con este tipo usando [`String.toFloat`][toFloat], al tratar de convertir texto introducido por el usuario a un n√∫mero. Veamos c√≥mo se usa:

<!-- prettier-ignore-start -->
{% replWithTypes %}
[
  {
    "input": "String.toFloat",
    "value": "\u001b[36m<function>\u001b[0m",
    "type_": "String -> Maybe Float"
  },
  {
    "input": "String.toFloat \"3.1415\"",
    "value": "\u001b[96mJust\u001b[0m \u001b[95m3.1415\u001b[0m",
    "type_": "Maybe Float"
  },
  {
    "input": "String.toFloat \"abc\"",
    "value": "\u001b[96mNothing\u001b[0m",
    "type_": "Maybe Float"
  }
]
{% endreplWithTypes %}
<!-- prettier-ignore-end -->

Prueba llamar `String.toFloat` con un otros textos y observa lo que te devuelve ‚¨ÜÔ∏è

No cualquier texto tiene sentido como un n√∫mero, por lo que esta funci√≥n modela esa situaci√≥n en forma expl√≠cita. ¬øPuede un `String` ser convertido a un `Float`? ¬°Tal vez (‚Äúmaybe‚Äù)! As√≠ podemos despu√©s usar b√∫squeda de patrones sobre el valor resultante y continuar seg√∫n sea apropiado.

> **Ejercicio:** Escrib√≠ un peque√±o programa que convierte grados Celcius a Fahrenheit, [aqu√≠](https://ellie-app.com/bJSMQz9tydqa1). Intenta refactorizar el c√≥digo `view` de distintas maneras. ¬øLe puedes poner un borde rojo al campo de texto cuando el valor es inv√°lido? ¬øPuedes a√±adir m√°s conversiones, como Fahrenheit a Celcius, o pulgadas a metros?

<!-- TODO: üëÜ Traducir este ejemplo. -->

[toFloat]: https://package.elm-lang.org/packages/elm-lang/core/latest/String#toFloat

## Campos opcionales

Otro lugar donde aparecen valores `Maybe` es en registros con campos opcionales.

Por ejemplo, digamos que administramos una red social. O sea, conectar gente, amistades, etc. Nuestro objetivo lo puso mejor el noticiero-parodia The Onion en 2011: [‚Äúrecolecta toda la informaci√≥n posible para la CIA‚Äù](https://youtu.be/juQcZO_WnsI). Y si queremos _toda_ la informaci√≥n de los usuarios, tenemos que persuadirlos de a poquito. Hay que poner funcionalidades que los impulse a ir compartiendo m√°s y m√°s informaci√≥n mientras m√°s usan nuestra red.

Empecemos con un modelo simple del usuario. Deben tener un nombre, pero vamos a dejar la edad como opcional.

```elm
type alias User =
    { name : String
    , age : Maybe Int
    }
```

Ahora imaginemos que Sue cre√≥ una cuenta, pero decide no compartir su fecha de nacimiento.

```elm
sue : User
sue =
    { name = "Sue", age = Nothing }
```

Los amigos de Sue no le pueden desear un feliz cumplea√±os. ¬øQue acaso son amigos realmente? Tom, despu√©s, crea una cuenta y _s√≠_ comparte su edad:

```elm
tom : User
tom =
    { name = "Tom", age = Just 24 }
```

Genial, seguro que para su cumplea√±os va a recibir muchas felicidades. Pero m√°s importantemente, Tom es parte de un valioso grupo etario. Los anunciantes van a estar contentos.

Muy bien, ya tenemos algunos usuarios, pero ¬øc√≥mo les vendemos alcohol sin romper la ley? Seguro se enojan con nosotros si les tratamos de vender a j√≥venes menores de 21, as√≠ que hagamos el chequeo:

```elm
canBuyAlcohol : User -> Bool
canBuyAlcohol user =
    case user.age of
        Nothing ->
            False

        Just age ->
            age >= 21
```

F√≠jate en que el tipo `Maybe` nos obliga a hacer b√∫squeda de patrones sobre la edad del usuario. Es, en realidad, imposible escribir c√≥digo donde nos olvidemos de que un usuario podr√≠a no tener una edad. Elm se cerciora de esto. Ahora podemos venderle alcohol a nuestros usuarios sabiendo que no estamos haci√©ndolo a menores de edad sin querer, sino s√≥lo a los mayores. √âxito.

## Evitando el sobreuso

Este tipo `Maybe` es muy √∫til, pero hay l√≠mites. Los principiantes tienen una particular tendencia a entusiasmarse con `Maybe` y ponerse a usarlo en todas partes, incluso cuando un tipo personalizado ser√≠a m√°s apropiado.

Por ejemplo, imaginemos que tenemos una aplicaci√≥n de ejercicio donde competimos con nuestros amigos. Partimos con una lista con s√≥lo los nombres de nuestros amigos, pero podemos cargar m√°s informaci√≥n sobre ellos despu√©s. Podr√≠a tentarte modelarlo de esta manera:

```elm
type alias Friend =
    { name : String
    , age : Maybe Int
    , height : Maybe Float
    , weight : Maybe Float
    }
```

Toda la informaci√≥n est√° ah√≠, pero no est√°s realmente modelando la forma en que funciona esta aplicaci√≥n. Ser√≠a mucho m√°s preciso modelarlo as√≠:

```elm
type Friend
    = Less String
    | More String Info


type alias Info =
    { age : Int
    , height : Float
    , weight : Float
    }
```

Este nuevo modelo captura mucho mejor la realidad de la aplicaci√≥n. S√≥lo tenemos dos posibles situaciones: O s√≥lo tienes el nombre, o tienes el nombre y un mont√≥n de informaci√≥n extra. En el c√≥digo de nuestra vista s√≥lo necesitamos pensar en si acaso vamos a mostrar una vista `Less` o `More` del amigo. No hace falta responder preguntas como ‚Äú¬øQu√© pasa si tengo `age` pero no tengo `weight`?‚Äù. Simplemente no es algo posible con nuestro tipo m√°s preciso.

El punto es que si te hallas usando `Maybe` en todas partes, vale la pena revisar tus definiciones `type` y `type alias` y preguntarte si puedes acaso usar una representaci√≥n m√°s exacta. Esto comunmente conllevar√° refactorizaciones que simplifican tu c√≥digo de actualizaci√≥n y de vista.

> ## Par√©ntesis: Relaci√≥n a referencias nulas (`null`)
>
> El inventor de las referencias `null`, Tony Hoare, dijo lo siguiente:
>
> > Lo llamo mi ‚Äúerror de los mil millones‚Äù. Hablo de la invenci√≥n de la referencia nula en 1965. En ese entonces estaba dise√±ando el primer sistema exhaustivo de tipos para referencias en un lenguaje orientado a objetos (ALGOL W). Mi objetivo era asegurarme de que el uso de cualquier referencia sea absolutamente seguro, con chequeos realizados en forma autom√°tica por el compilador. Pero no pude resistir la tentaci√≥n de a√±adir referencias nulas, s√≥lo porque eran tan f√°ciles de implementar. Esto conllev√≥ a un sinf√≠n de errores, vulnerabilidades y ca√≠das de sistema, los que probablemente han causado miles de millones de d√≥lares en sufrimiento y da√±os durante estos cuarenta a√±os.
>
> Es un dise√±o que hace que un error sea **impl√≠cito**. En cualquier momento que creas que tienes un `String`, podr√≠as en realidad tener `null`. ¬øTenemos que revisarlo? ¬øEl c√≥digo que te pas√≥ el valor hizo el chequeo? Tal vez no haya problema; tal vez haga que se caiga tu servidor. Supongo que nos enteraremos despu√©s.
>
> Elm evita estos problemas al simplemente no tener referencias `null`. En cambio, tenemos tipos personalizados como `Maybe` para que los errores sean **expl√≠citos**. As√≠ no nos topamos con sorpresas. Un `String` siempre ser√° un `String`, y cuando veas `Maybe String`, el compilador te asegura que ambas variantes est√°n siendo consideradas en el c√≥digo. As√≠ obtenemos la misma flexibilidad, pero ninguno de los bugs.

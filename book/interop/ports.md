# Puertos

Los puertos permiten la comunicaci√≥n entre Elm y JavaScript.

Son m√°s frecuentemente usados con [`WebSockets`](https://github.com/elm-community/js-integration-examples/tree/master/websockets) y con [`localStorage`](https://github.com/elm-community/js-integration-examples/tree/master/localStorage). Enfoqu√©monos en el ejemplo de `WebSockets`.

<!-- TODO: üëÜ Agregar estos ejemplos al repositorio y traducirlos. -->

## Puertos en JavaScript

Aqu√≠ tenemos pr√°cticamente el mismo HTML que hemos usado en las √∫ltimas dos p√°ginas, pero con un poco de JavaScript a√±adido. Creamos una conexi√≥n con `wss://echo.websocket.org` que repite lo que sea que le env√≠es. Puedes comprobar viendo [este ejemplo](https://ellie-app.com/8yYgw7y7sM2a1) que nos permite crear el esqueleto de un chat:

<!-- TODO: üëÜ Traducir el ejemplo en el Ellie, y actualizar el link y el c√≥digo abajo. -->

```html
<!doctype html>
<html>
  <head>
    <meta charset="UTF-8" />
    <title>Elm + Websockets</title>
    <script type="text/javascript" src="elm.js"></script>
  </head>

  <body>
    <div id="myapp"></div>
  </body>

  <script type="text/javascript">
    // Start the Elm application.
    var app = Elm.Main.init({
      node: document.getElementById("myapp"),
    });

    // Create your WebSocket.
    var socket = new WebSocket("wss://echo.websocket.org");

    // When a command goes to the `sendMessage` port, we pass the message
    // along to the WebSocket.
    app.ports.sendMessage.subscribe(function (message) {
      socket.send(message);
    });

    // When a message comes into our WebSocket, we pass the message along
    // to the `messageReceiver` port.
    socket.addEventListener("message", function (event) {
      app.ports.messageReceiver.send(event.data);
    });

    // If you want to use a JavaScript library to manage your WebSocket
    // connection, replace the code in JS with the alternate implementation.
  </script>
</html>
```

Llamamos `Elm.Main.init()` igual que en otros ejemplos de interoperabilidad, pero esta vez s√≠ usamos el objeto `app` que devuelve. Nos suscribimos al puerto `sendMessage` y estamos enviando datos al puerto `messageReceiver`.

Estos tienen una correspondencia en el lado Elm.

## Puertos en Elm

Revisa las l√≠neas en que usamos la palabra clave `port` en el archivo Elm correspondiente. As√≠ es como definimos los puertos que acabamos de ver en el lado JavaScript.

```elm
port module Main exposing (..)

import Browser
import Html exposing (..)
import Html.Attributes exposing (..)
import Html.Events exposing (..)
import Json.Decode as D



-- MAIN


main : Program () Model Msg
main =
    Browser.element
        { init = init
        , view = view
        , update = update
        , subscriptions = subscriptions
        }



-- PORTS


port sendMessage : String -> Cmd msg


port messageReceiver : (String -> msg) -> Sub msg



-- MODEL


type alias Model =
    { draft : String
    , messages : List String
    }


init : () -> ( Model, Cmd Msg )
init flags =
    ( { draft = "", messages = [] }
    , Cmd.none
    )



-- UPDATE


type Msg
    = DraftChanged String
    | Send
    | Recv String



-- Use the `sendMessage` port when someone presses ENTER or clicks
-- the "Send" button. Check out index.html to see the corresponding
-- JS where this is piped into a WebSocket.


update : Msg -> Model -> ( Model, Cmd Msg )
update msg model =
    case msg of
        DraftChanged draft ->
            ( { model | draft = draft }
            , Cmd.none
            )

        Send ->
            ( { model | draft = "" }
            , sendMessage model.draft
            )

        Recv message ->
            ( { model | messages = model.messages ++ [ message ] }
            , Cmd.none
            )



-- SUBSCRIPTIONS
--
-- Subscribe to the `messageReceiver` port to hear about messages coming in
-- from JS. Check out the index.html file to see how this is hooked up to a
-- WebSocket.


subscriptions : Model -> Sub Msg
subscriptions _ =
    messageReceiver Recv



-- VIEW


view : Model -> Html Msg
view model =
    div []
        [ h1 [] [ text "Echo Chat" ]
        , ul []
            (List.map (\msg -> li [] [ text msg ]) model.messages)
        , input
            [ type_ "text"
            , placeholder "Draft"
            , onInput DraftChanged
            , on "keydown" (ifIsEnter Send)
            , value model.draft
            ]
            []
        , button [ onClick Send ] [ text "Send" ]
        ]



-- DETECT ENTER


ifIsEnter : msg -> D.Decoder msg
ifIsEnter msg =
    D.field "key" D.string
        |> D.andThen
            (\key ->
                if key == "Enter" then
                    D.succeed msg

                else
                    D.fail "some other key"
            )
```

F√≠jate en que la primera l√≠nea dice `port module` en vez de s√≥lo `module`. Esto es lo que hace posible definir puertos dentro del m√≥dulo. El compilador te va a dar ayuda si acaso te equivocas en esto, as√≠ que espero que no llegue a ser un problema muy grande.

Bueno, pero ¬øqu√© significan las declaraciones `port` para `sendMessage` y `messageReceiver`?

## Mensajes de salida (`Cmd`)

La declaraci√≥n de `sendMessage` nos permite enviar mensajes de salida desde Elm.

```elm
port sendMessage : String -> Cmd msg
```

Aqu√≠ declaramos que queremos enviar valores `String`, pero podr√≠amos poner cualquiera de los otros tipos que funcionan con flags. Hablamos sobre esos tipos en la p√°gina anterior, y puedes revisar este [ejemplo que usa `localStorage`](https://ellie-app.com/8yYddD6HRYJa1) para ver c√≥mo enviamos un valor [`Json.Encode.Value`](https://package.elm-lang.org/packages/elm/json/latest/Json-Encode#Value) hacia JavaScript.

<!-- TODO: üëÜ Traducir este ejemplo en el Ellie. -->

Ahora podemos usar `sendMessage` igual que cualquier funci√≥n. Si tu funci√≥n `update` genera un comando `sendMessage "hello"`, lo vas a recibir en el lado de JavaScript:

```javascript
app.ports.sendMessage.subscribe(function (message) {
  socket.send(message);
});
```

Este c√≥digo JavaScript est√° suscrito a todos los mensajes de salida. Puedes llamar suscribir m√∫ltiples funciones con `subscribe`, y despu√©s desuscribirlas por referencia usando `unsubscribe`, pero en general sugerimos mantener esto est√°tico.

Tambi√©n recomendamos enviar mensajes m√°s completos en vez de crear muchos puertos individuales. Tal vez eso significar√≠a tener un tipo personalizado en Elm que representa todo lo que necesites decirle a JS, y despu√©s usar [`Json.Encode`](https://package.elm-lang.org/packages/elm/json/latest/Json-Encode) para enviarlo a una √∫nica suscripci√≥n de JS. Mucha gente cree que esto contribuye a tener una mejor separaci√≥n de intereses. El c√≥digo Elm es claramente due√±o de cierto estado, y el lado JS es claramente due√±o de cierto otro estado.

## Mensajes de entrada (`Sub`)

La declaraci√≥n de `messageReceiver` nos permite escuchar mensajes que entran a Elm.

```elm
port messageReceiver : (String -> msg) -> Sub msg
```

Aqu√≠ decimos que vamos a recibir valores `String`, pero nuevamente, podemos escuchar cualquier tipo que sea compatible con flags o con puertos de salida. Simplemente cambia el tipo `String` por el de uno de los tipos que puede cruzar la frontera.

Podemos usar `messageReceiver` igual que otras funciones. En nuestro caso, llamamos `messageReceiver Recv` cuando definimos nuestras suscripciones, porque queremos escuchar cualquier mensaje de entrada desde JavaScript. Esto nos permitir√° recibir mensajes como `Recv "¬øc√≥mo est√°s?"` en nuestra funci√≥n `update`.

En el lado JavaScript, podemos enviar cosas a un puerto cuando queramos:

```javascript
socket.addEventListener("message", function (event) {
  app.ports.messageReceiver.send(event.data);
});
```

En este caso lo estamos haciendo cuando recibimos un mensaje v√≠a un websocket, pero podr√≠as enviarlo en cualquier otro momento tambi√©n. Tal vez hay otra fuente m√°s desde la cual recibimos mensajes. No hay problema, Elm no necesita saber los detalles. S√≥lo m√°ndale el string por el puerto que corresponda.

## Notas

**Los puertos est√°n dise√±ados para crear fronteras.** Definitivamente no intentes crear un puerto por cada funci√≥n JS que necesites. Tal vez te gusta mucho Elm y quieras hacer todo en Elm sin importar el costo, pero los puertos no est√°n dise√±ados para eso. En vez de esto, enf√≥cate en preguntas como ‚Äú¬øqui√©n es el due√±o de este estado?‚Äù, y usa uno o dos puertos para enviar mensajes de ida y vuelta. Si est√°s en un escenario complejo, puedes incluso simular valores `Msg` enviando objetos JS como `{ tag: "active-users-changed", list: ... }`, donde tienes una etiqueta para cada variante de la informaci√≥n que necesites transmitir.

Aqu√≠ tienes algunas reglas generales y problemas frecuentes:

- **Es recomendado enviar `Json.Encode.Value` en tus puertos.** Igual que con flags, hay ciertos tipos b√°sicos que pueden transmitirse v√≠a puertos. Esto viene del tiempo antes de que existieran decodificadores de JSON, y puedes leer m√°s al respecto [aqu√≠](/interop/flags.html#verifying-flags).

- **Todas las declaraciones `port` deben aparecer en un `port module`.** Probablemente lo mejor es organizar tus puertos en un s√≥lo `port module` para que sea m√°s f√°cil visualizar la interfaz, toda en un s√≥lo lugar.

- **Los puertos son para aplicaciones.** Los `port module` est√°n disponibles para aplicaciones, pero no para paquetes. Esto asegura que los autores de una aplicaci√≥n tengan la flexibilidad que necesitan, pero el ecosistema de paquetes est√° escrito en Elm al cien porciento. Creemos que esto crear√° un ecosistema y una comunidad m√°s fuertes a la larga, y nos referimos a los sacrificios involucrados m√°s en detalle en la secci√≥n siguiente sobre los [l√≠mites](/interop/limits.html) de la interoperabilidad Elm/JS.

- **Los puertos pueden ser eliminados como c√≥digo muerto.** Elm tiene un agresivo sistema de [eliminaci√≥n de c√≥digo muerto](/interop/limits.html), y borrar√° puertos que no son usados dentro de Elm, ya que el compilador no tiene idea de lo que ocurre en el lado JavaScript. Por lo tanto, intenta cablear tus puertos en el lado Elm antes que nada.

Espero que esta informaci√≥n te ayude a encontrar maneras de incluir Elm junto a tu JavaScript preexistente. No es tan excitante como hacer una reescritura completa en Elm, pero hist√≥ricamente hemos visto que es una estrategia mucho m√°s efectiva.

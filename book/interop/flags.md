# Flags

Las ‚Äúflags‚Äù son una forma de pasarle valores a Elm al inicializar.

Algunos usos comunes incluye pasarle llaves de API, variables de entorno y datos de usuario. Esto puede ser √∫til si generaste el HTML din√°micamente. Tambi√©n nos pueden ayudar a cargar informaci√≥n en cach√©, como en [este ejemplo de `localStorage`](https://github.com/elm-community/js-integration-examples/tree/master/localStorage).

<!-- TODO: üëÜ Agregar este ejemplo al repositorio y traducirlo. -->

## Flags en HTML

El HTML es pr√°cticamente igual a como lo vimos antes, pero con un argumento `flags` adicional que le pasamos a la funci√≥n `Elm.Main.init()`.

```html
<html>
  <head>
    <meta charset="UTF-8" />
    <title>Main</title>
    <script src="main.js"></script>
  </head>

  <body>
    <div id="myapp"></div>
    <script>
      var app = Elm.Main.init({
        node: document.getElementById("myapp"),
        flags: Date.now(),
      });
    </script>
  </body>
</html>
```

En este ejemplo le pasamos el tiempo actual en milisegundos, pero cualquier valor JS que pueda ser convertido en JSON puede ser usado como flag.

> **Nota:** Estos datos adicionales se llaman ‚Äúflags‚Äù porque es el t√©rmino que se usa para referirse a las opciones suministradas a un programa de l√≠nea de comandos. Cuando llamas `elm make src/Main.elm` tambi√©n le puedes pasar algunas ‚Äúflags‚Äù como `--optimize` o `--output=main.js` para cambiar lo que har√°. En este caso es lo mismo.

## Flags en Elm

Para recibir las flags en el lado de Elm tendr√°s que modificar un poco la funci√≥n `init`:

```elm
module Main exposing (..)

import Browser
import Html exposing (Html, text)



-- MAIN


main : Program Int Model Msg
main =
    Browser.element
        { init = init
        , view = view
        , update = update
        , subscriptions = subscriptions
        }



-- MODEL


type alias Model =
    { currentTime : Int }


init : Int -> ( Model, Cmd Msg )
init currentTime =
    ( { currentTime = currentTime }
    , Cmd.none
    )



-- UPDATE


type Msg
    = NoOp


update : Msg -> Model -> ( Model, Cmd Msg )
update _ model =
    ( model, Cmd.none )



-- VIEW


view : Model -> Html Msg
view model =
    text (String.fromInt model.currentTime)



-- SUBSCRIPTIONS


subscriptions : Model -> Sub Msg
subscriptions _ =
    Sub.none
```

Lo √∫nico importante aqu√≠ es que la funci√≥n `init` dice que recibe un argumento `Int`. Esta es la manera en que Elm puede acceder inmediatamente a las flags que le pases desde JavaScript. Despu√©s de eso puedes poner los datos en el modelo o ejecutar alg√∫n comando, lo que sea que necesites hacer.

Te recomiendo que revises [este ejemplo que usa `localStorage`](https://github.com/elm-community/js-integration-examples/tree/master/localStorage) para ver un caso m√°s interesante que usa flags.

<!-- TODO: üëÜ Agregar este ejemplo al repositorio y traducirlo. -->

## Verificando las flags

Pero, ¬øqu√© pasa si `init` dice que recibe una flag `Int`, y alguien trata de inicializar con `Elm.Main.init({ flags: "ja, ¬øqu√© vas a hacer?" })`?

Elm revisa esos casos, asegurando que las flags son exactamente lo que esperas. Sin este chequeo, le podr√≠as pasar cualquier cosa, y terminar√≠as viendo errores en tiempo de ejecuci√≥n en Elm.

Hay muchos tipos que pueden usarse en flags:

- `Bool`
- `Int`
- `Float`
- `String`
- `Maybe`
- `List`
- `Array`
- tuples
- records
- [`Json.Decode.Value`](https://package.elm-lang.org/packages/elm/json/latest/Json-Decode#Value)

Mucha gente usa `Json.Decode.Value` porque les da un control m√°s preciso. Pueden escribir un decodificador para lidiar con cualquier situaci√≥n espec√≠fica usando c√≥digo Elm, y recuper√°ndose sin problemas si llegan datos extra√±os.

Los otros tipos que Elm soporta son un rezago de antes de que tuvi√©ramos una forma de escribir decodificadores JSON. Si decides usarlos, hay ciertos detalles a tener en cuenta. Los siguientes ejemplos muestran el tipo deseado de la flag, y sus subpuntos ilustran la manera en que se interpretar√≠an distintos valores JS:

- `init : Int -> ...`

  - `0` => `0`
  - `7` => `7`
  - `3.14` => error
  - `6.12` => error

- `init : Maybe Int -> ...`

  - `null` => `Nothing`
  - `42` => `Just 42`
  - `"hi"` => error

- `init : { x : Float, y : Float } -> ...`

  - `{ x: 3, y: 4, z: 50 }` => `{ x = 3, y = 4 }`
  - `{ x: 3, name: "Tom" }` => error
  - `{ x: 360, y: "why?" }` => error

- `init : (String, Int) -> ...`
  - `["Tom", 42]` => `("Tom", 42)`
  - `["Sue", 33]` => `("Sue", 33)`
  - `["Bob", "4"]` => error
  - `["Joe", 9, 9]` => error

N√≥tese que si la conversi√≥n no sale bien, **vas a obtener un error de JS**. Estamos tomando la pol√≠tica de ‚Äúfallar r√°pido‚Äù. En vez de permitir que el error permee tu c√≥digo Elm, lo reportamos lo antes posible. Esta es otra raz√≥n por la que hay gente a la que le gusta usar `Json.Decode.Value` en sus flags. En vez de obtener un error de JS, el valor extra√±o pasa por un decodificador, garantizando que puedas implementar un comportamiento de contingencia.
